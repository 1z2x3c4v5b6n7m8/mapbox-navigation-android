apply plugin: 'maven-publish'
apply plugin: 'com.mapbox.sdkRegistry'
apply plugin: 'com.jfrog.bintray'
apply from: file("${rootDir}/gradle/artifact-settings.gradle")

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId project.ext.mapboxArtifactGroupId
                artifactId project.ext.mapboxArtifactId
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidJavadocsJar)

                pom.withXml {
                    final mainNode = asNode()
                    mainNode.appendNode('name', project.ext.mapboxArtifactTitle)
                    mainNode.appendNode('description', project.ext.mapboxArtifactDescription)
                    mainNode.appendNode('url', project.ext.mapboxArtifactUrl)

                    final licenseNode = mainNode.appendNode('licenses').appendNode('license')
                    licenseNode.appendNode('name', project.ext.mapboxArtifactLicenseName)
                    licenseNode.appendNode('url', project.ext.mapboxArtifactLicenseUrl)
                    licenseNode.appendNode('distribution', "repo")

                    final developerNode = mainNode.appendNode('developers').appendNode('developer')
                    developerNode.appendNode('id', project.ext.mapboxDeveloperId)
                    developerNode.appendNode('name', project.ext.mapboxDeveloperName)

                    final scmNode = mainNode.appendNode("scm")
                    scmNode.appendNode("connection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("developerConnection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("url", project.ext.mapboxArtifactUrl)
                }
            }
            debug(MavenPublication) {
                from components.debug
                groupId project.ext.mapboxArtifactGroupId
                artifactId project.ext.mapboxArtifactId
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidJavadocsJar)
            }
        }
    }
}

def sdkNameMap = [:]
sdkNameMap["libnavigation-base"] = "mobile-navigation-base"
sdkNameMap["libnavigation-core"] = "mobile-navigation-core"
sdkNameMap["libnavigation-metrics"] = "mobile-navigation-metrics"
sdkNameMap["libnavigator"] = "mobile-navigation-navigator"
sdkNameMap["libtrip-notification"] = "mobile-navigation-notification"
sdkNameMap["libnavigation-router"] = "mobile-navigation-router"
sdkNameMap["libnavigation-util"] = "mobile-navigation-utils"
sdkNameMap["libnavigation-ui"] = "mobile-navigation-ui"
sdkNameMap["librouteline"] = "mobile-navigation-ui-routeline"

registry {
    sdkName = sdkNameMap[project.name]
    production = true
    snapshot = project.ext.versionName.endsWith("-SNAPSHOT")
    override = snapshot ? true : false
    dryRun = false
    publications = ["release"]
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.kotlin
}

task androidJavadocsJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}


ext {
    mapboxArtifactId = project.property('POM_ARTIFACT_ID')
    mapboxArtifactTitle = project.property('POM_ARTIFACT_TITLE')
    mapboxDeveloperName = 'Mapbox'
    mapboxDeveloperId = 'mapbox'
    versionName = project.property('POM_SNAPSHOT_VERSION_NAME')

    mapboxBintrayUserOrg = 'mapbox'
    mapboxBintrayRepoName = 'mapbox_collab'
    mapboxBintrayPackageName = "com.mapbox.navigation:" + project.property('POM_ARTIFACT_ID')
    mapboxBintrayUser = project.hasProperty('BINTRAY_USER') ? project.property('BINTRAY_USER') : System.getenv('BINTRAY_USER')
    mapboxBintrayApiKey = project.hasProperty('BINTRAY_API_KEY') ? project.property('BINTRAY_API_KEY') : System.getenv('BINTRAY_API_KEY')
    mapboxGpgPassphrase = project.hasProperty('GPG_PASSPHRASE') ? project.property('GPG_PASSPHRASE') : System.getenv('GPG_PASSPHRASE')
}

bintray {
    user = mapboxBintrayUser
    key = mapboxBintrayApiKey
    publications('release')
    pkg {
        repo = project.ext.mapboxBintrayRepoName
        name = project.ext.mapboxBintrayPackageName
        userOrg = project.ext.mapboxBintrayUserOrg
        publish = false
        version {
            name = project.ext.versionName
            released = new Date()
            gpg {
                sign = true
                passphrase = mapboxGpgPassphrase
            }
            mavenCentralSync {
                sync = false
            }
        }
    }
}